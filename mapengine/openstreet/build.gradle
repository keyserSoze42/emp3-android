apply plugin: 'java'
apply plugin: 'nebula.maven-base-publish'
apply plugin: 'nebula.provided-base'

dependencies {
    compile          ("com.android.support:support-annotations")
    provided         (group: 'sec.web.renderer', name: 'mil-sym-android-renderer', ext: 'jar') { transitive = true }
    provided project (":sdk:emp3-android-sdk")

    compile          (group: 'org.osmdroid', name: 'osmdroid-android', version: '5.1', ext: 'aar') { transitive = true }
    compile          (group: 'com.github.MKergall.osmbonuspack', name: 'OSMBonusPack', version: 'v5.7', ext: 'aar') { transitive = true }
}

publishing.publications {
    nebula(MavenPublication) {
        groupId = project.ext.group
        artifactId = "emp3-android-openstreet"
        version = project.version
        artifacts = [
                "$buildDir/../aar/build/outputs/aar/mapengine-openstreet-aar-release.aar",
                "$buildDir/../apk/build/outputs/apk/mapengine-openstreet-apk-debug.apk"
        ]

        pom.withXml {
            configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.each { dep ->
                asNode().dependencies[0].dependency.each {

                    if (it.artifactId[0].text() == dep.moduleName &&
                            it.groupId[0].text() == dep.moduleGroup &&
                            it.scope[0].text() != "provided") {

                        it.scope[0].value = "compile"
                    }
                }
            }
        }
    }
}

publishing.repositories {
    maven {
        url =  nexusPublishUrl

        credentials {
            username project.di2eNexusUsername
            password project.di2eNexusPassword
        }
    }
}


